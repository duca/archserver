# Maintainer: # Maintainer: duca < edumlopes >
pkgname=openmpi
pkgver=1.5
pkgrel=2
pkgdesc="High performance message passing library (MPI)"
arch=('i686' 'x86_64')
url="http://www.open-mpi.org"
license=('BSD')
depends=('gcc' 'gcc-fortran' 'openssh' 'valgrind')
options=(!libtool)
source=(http://www.open-mpi.org/software/ompi/v1.5/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('8d89a5a666ccc8f174f6ee9b26ba231698ca8e68')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   ./configure --prefix=/usr \
               --sysconfdir=/etc/${pkgname} \
               --mandir=/usr/share/man \
               --enable-mpi-f90 \
               --libdir=/usr/lib/${pkgname} \
               --with-threads=posix \
               --enable-mpi-threads \
               --enable-smp-locks \
               --with-valgrind \
               --enable-pretty-print-stacktrace \
               --without-slurm \
               FC=/usr/bin/gfortran \
               LDFLAGS='-Wl,-z,noexecstack' || return 1

   make -j2 || return 1
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR=${pkgdir} install || return 1

   # openmpi's otfinfo conflicts with the one from texlive
   mv ${pkgdir}/usr/bin/otfinfo ${pkgdir}/usr/bin/otfinfompi || return 1

   # openmpi's otfdump conflicts with the one from libotf
   mv ${pkgdir}/usr/bin/otfdump ${pkgdir}/usr/bin/otfdumpompi || return 1

   install -d -m 755 ${pkgdir}/etc/ld.so.conf.d || return 1
   echo "/usr/lib/${pkgname}" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf || return 1
}
